apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def ext = rootProject.ext

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion ext.compileSdkVersion
    buildToolsVersion ext.buildToolsVersion

    defaultConfig {
        applicationId ext.applicationId
        minSdkVersion ext.minSdkVersion
        targetSdkVersion ext.targetSdkVersion
        versionCode ext.versionCode
        versionName ext.versionName
        multiDexEnabled true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    signingConfigs {
        release {
            try {
                storeFile file("../daily.jks")
                storePassword KEYSTORE_PWD
                keyAlias KEY_ALIAS
                keyPassword KEY_PWD
            } catch (ex) {
                throw new InvalidUserDataException("Have to define KEYSTORE_PWD and KEY_PWD.")
            }
        }
    }

    productFlavors {
        daily {}
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            zipAlignEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // goin_v1.0.0_2015-07-30_xiaomi.apk
                        def fileName = "daily_v${ext.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    dataBinding {
        enabled = true
    }

//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
//    }
}

dependencies {

    def dependencies = ext.dependencies

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile dependencies.supportAppCompat
    compile dependencies.supportRecyclerView
    compile dependencies.supportDesgin
    compile dependencies.supportCardView
    compile dependencies.glide
    compile dependencies.butterknife
    compile dependencies.logansquare
    compile dependencies.viewpagerIndicator
    compile dependencies.guangqiangWidgets
    compile dependencies.guangqiangUtils
    compile dependencies.retrofit
    compile dependencies.retrofitAdapter
    compile dependencies.okhttpLog
    compile dependencies.logansquareConverter
    compile dependencies.rxandroid
    compile dependencies.rxjava
    compile dependencies.eventbus
    compile dependencies.dagger

    provided dependencies.daggerCompiler
    apt dependencies.logansquareCompiler

    provided 'org.glassfish:javax.annotation:10.0-b28'
}
